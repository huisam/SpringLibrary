plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'net.ltgt.apt' version '0.20'
    id "com.moowork.node" version "1.3.1"
    id 'java'
}

apply plugin: 'net.ltgt.apt-idea'


group = 'com.huisam'
version = 'huisam.1'
sourceCompatibility = '14'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-mustache'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.789'
    implementation 'com.github.derjust:spring-data-dynamodb:5.1.0'

    implementation platform('software.amazon.awssdk:bom:2.13.72')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:netty-nio-client'
    implementation 'software.amazon.awssdk:cloudwatch-metric-publisher:2.13.52-PREVIEW'

    implementation 'org.apache.commons:commons-lang3:3.9'
    runtimeOnly 'com.h2database:h2'

    compileOnly 'org.mapstruct:mapstruct:1.3.1.Final'
    annotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'


    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.10.4'
    testImplementation 'org.xmlunit:xmlunit-assertj:2.6.4'
    testImplementation 'org.xmlunit:xmlunit-core:2.6.4'
    testImplementation 'org.xmlunit:xmlunit-matchers:2.6.4'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'io.projectreactor:reactor-tools'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

test {
    useJUnitPlatform()
}

// setting for FrontEnd
node {
    version = '12.13.1'
    npmVersion = '6.12.1'
    workDir = file('./src/frontend')
    npmWorkDir = file('./src/frontend')
    nodeModulesDir = file('./src/frontend')
}

task setUp(type: NpmTask) {
    description = 'Install Node.js packages'
    args = ['install']
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task buildFrontEnd(type: NpmTask, dependsOn: setUp) {
    description = 'Build vue.js'
    args = ['run', 'build']
}

processResources.dependsOn 'buildFrontEnd'
